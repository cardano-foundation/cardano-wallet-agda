# Specification: Customer Deposit Wallet

## Synopsis

üöß DRAFT 2025-01-20

This document specifies the core functionality of a **customer deposit wallet**,
or **deposit wallet** for short.

A deposit wallet allows you to track the **origin of incoming funds**:
Each customer is assigned a unique address belonging to the wallet;
a deposit made at this address is treated as originating from the customer.

However, outgoing funds are not distinguished by customer
‚Äî once funds have gone into the wallet,
they are all part of a **single wallet balance**.

A deposit wallet is useful for businesses such
as centralized exchanges (CEX) or e-shops on Cardano.

Technically, each customer is represented by a numerical index starting at `0`.
The deposit wallet manages a mapping between indices and addresses,
and tracks incoming funds for each known address.

# Setup

This document is a [literate Agda][lagda] file: It contains prose that
describes and explains the specification, but it also contains definitions
and logical properties that can be checked by the proof assistant [Agda][].

When implementing this specification,
we intend to create a **machine-checked proof**
that our implementation matches this specification.
However, this specification only covers the **core functionality** of
the software application to be implemented, not the full software.
We proceed as follows:

* The full software application will be implemented in [Haskell][].
  Sizeable parts of the functionality will be tested, not proven.

* However, the core functionality that is covered by this specification
  will be implemented using [Agda][] and exported to Haskell
  via the [agda2hs][] transpiler.
  This core functionality will be proven, not tested.

* In turn, proofs about the core functionality will depend on the
  assumption that more basic data types provided by Haskell libraries,
  such as the [Data.Set][containers] type,
  have implementations that match a specification.
  For the time being, we accept that this is an assumption
  and that the library implementations have only been tested.
  We `postulate` specifications in Agda as far as needed.

  [agda]: https://github.com/agda/agda
  [lagda]: https://agda.readthedocs.io/en/v2.6.4/tools/literate-programming.html
  [agda2hs]: https://github.com/agda/agda2hs
  [haskell]: https://www.haskell.org
  [containers]: https://hackage.haskell.org/package/containers-0.7/docs/Data-Set.html

```agda
module Specification where
```

## Imports

In order to formulate the specification, we need to import standard Haskell vocabulary

```agda
open import Haskell.Prelude
open import Haskell.Reasoning
```

and also

* [Specification.Common](Specification/Common.lagda.md)
  ‚Äî Minor additional Haskell concepts.

In addition, we also need to import concepts that are specific to Cardano:

* [Specification.Cardano](Specification/Cardano.lagda.md)
  * [Specification.Cardano.Tx](Specification/Cardano/Value.lagda.md)
  ‚Äî Transaction type `Tx`.
  * [Specification.Cardano.Value](Specification/Cardano/Value.lagda.md)
  ‚Äî Monetary `Value`. Includes both native coins (ADA) and
user-defined assets, such as stablecoins or NFTs.

<!--
```agda
open import Haskell.Data.Word.Odd using (Word31)

open import Specification.Common using
  (_‚áî_; _‚àà_; isSubsetOf; isJust; nub)

import Specification.Cardano
import Specification.Wallet.UTxO
```
-->

# Specification

## Overview

This specification of a **customer deposit wallet**
amounts to the specification of an abstract data type `WalletState`,
which represents the entire state of such a wallet.

The goal of this document is to specify the operations
on this abstract data type and the logical properties that relate them.

We define a `module` `DepositWallet` which is parametrized by

* the abstract data type `WalletState` that we wish to specify,
* an implementation `SigCardano` of concepts that are related to Cardano,
and which we need to express the specification.
* a specification `SigWallet` of wallet-related concepts that are
  not the focus of this document, and

```agda
module
  DepositWallet
    (WalletState : Set)
    (XPub : Set)
    (SigCardano : Specification.Cardano.Signature)
    (SigWallet  : Specification.Wallet.UTxO.Signature SigCardano)
  where

  open Specification.Cardano.Signature SigCardano
  open Specification.Wallet.UTxO.Signature SigWallet
```

## Operations

We now list all auxiliary data types and all
operations supported by the abstract data type `WalletState`.
This list is meant for reference
‚Äî we will explain each of them in detail in the subsequent sections.

Auxiliary data types:

```agda
  Customer = Word31

  record ValueTransfer : Set where
    field
      spent    : Value
      received : Value

  open ValueTransfer

  TxSummary : Set
  TxSummary = Slot √ó TxId √ó ValueTransfer
```

Operations:

```agda
  record Operations : Set where
    field

      deriveCustomerAddress : XPub ‚Üí Customer ‚Üí Address
      fromXPubAndMax        : XPub ‚Üí Word31 ‚Üí WalletState
      listCustomers         : WalletState ‚Üí List (Customer √ó Address)

      totalUTxO : WalletState ‚Üí UTxO
      applyTx   : Tx ‚Üí WalletState ‚Üí WalletState
      isOurs    : WalletState ‚Üí Address ‚Üí Bool

      getCustomerHistory : WalletState ‚Üí Customer ‚Üí List TxSummary

      createPayment
        : List (Address √ó Value)
        ‚Üí PParams ‚Üí WalletState ‚Üí Maybe TxBody
```

## Properties

In subsequent sections, we will specify the properties that
the operations should satisfy.

The following record collects the properties:

```agda
  record Properties (O : Operations) : Set‚ÇÅ where
    open Operations O
```

### Mapping between Customers and Address

The type `Customer` denotes a unique identifier for a customer.
For reasons explained later, we choose to represent this type
as numerical indices:

    Customer = Word31

The mapping between customers and addresses is maintained by
the following operations:

    deriveCustomerAddress : XPub ‚Üí Customer ‚Üí Address

    fromXPubAndMax        : XPub ‚Üí Word31 ‚Üí WalletState
    listCustomers         : WalletState ‚Üí List (Customer √ó Address)

Here

* `deriveCustomerAddress` deterministically creates an address
  for a given customer index.

* `fromXPubAndMax xpub cmax` creates an empty `WalletState`
  at genesis which keeps track of customers indices `0` to `cmax`.
  Their addresses are derived deterministically from the public key `xpub`.

* `listCustomers` returns the mapping between customers and addresses
  currently maintained by the `WalletState`.

In order to make the specification clear and simple,
we do not allow the mapping between customers and addresses
to change after creation
‚Äî the idea is that `listCustomers` will always return the same result,
no matter how the `WalletState` is changed subsequently.

The result of the function `listCustomers` contains
the entire mapping between customers and addresses.
The following definitions make this mapping easier to discuss:

```agda
    customerAddress : Customer ‚Üí WalletState ‚Üí Maybe Address
    customerAddress c = lookup c ‚àò listCustomers

    knownCustomer : Customer ‚Üí WalletState ‚Üí Bool
    knownCustomer c = isJust ‚àò customerAddress c

    knownCustomerAddress : Address ‚Üí WalletState ‚Üí Bool
    knownCustomerAddress a = elem a ‚àò map snd ‚àò listCustomers
```

We require that the mapping is a bijection

```agda
    unique : {{Eq a}} ‚Üí List a ‚Üí Bool
    unique xs = nub xs == xs

    isBijection : ‚àÄ {{_ : Eq a}} {{_ : Eq b}} ‚Üí List (a √ó b) ‚Üí Bool
    isBijection xys = unique (map fst xys) && unique (map snd xys)

    field
      prop-listCustomers-isBijection
        : ‚àÄ (s : WalletState)
        ‚Üí isBijection (listCustomers s) ‚â° True
```

The relation between `listCustomers` and `fromXPubAndMax`
is specified as follows:

First, the mapping precisely contains customers indices `0` to `count - 1`:

```agda
      prop-listCustomers-fromXPubAndMax-max
        : ‚àÄ (c : Customer) (xpub : XPub) (cmax : Word31)
        ‚Üí knownCustomer c (fromXPubAndMax xpub cmax)
          ‚â° (0 <= c && c <= cmax)
```

Second, the addresses are derived deterministically from
the public key and the customer index.

```agda
      prop-listCustomers-fromXPubAndMax-xpub
        : ‚àÄ (c : Customer) (xpub : XPub) (cmax : Word31) (addr : Address)
        ‚Üí customerAddress c (fromXPubAndMax xpub cmax)
          ‚â° Just addr
        ‚Üí deriveCustomerAddress xpub c
          ‚â° addr
```

The idea is that these properties hold not only for the initial state
at `fromXPubAndMax`, but also for any state obtained through
other operations such as `applyTx`.

For compatibility with hardware wallets and the [BIP-32][] standard,
we derive the `Address` of each customer from the root private key
of the wallet in a deterministic fashion.
Specifically, using the notation of [BIP-32][] in pseudo-code,
we require that

  	deriveCustomerAddress : WalletState ‚Üí Word31 ‚Üí Address
	  deriveCustomerAddress s ix = rootXPrv s / 1857' / 1815' / 0' / 0 / ix

Here, `1857` is a new ‚Äúpurpose‚Äù identifier; we cannot reuse the [CIP-1852][] standard, because it behaves differently when discovering funds in blocks.

  [bip-32]: https://github.com/bitcoin/bips/blob/master/bip-0032.mediawiki
  [cip-1852]: https://cips.cardano.org/cips/cip1852/

This method of deriving addresses is also the reason why we choose
a concrete representation of `Customer` as `Word31`.

### Wallet balance and transactions

The primary purpose of a wallet is to keep track of funds
that are available for spending.

On the Cardano blockchain,
this means keeping track of unspent transaction outputs (UTxO).
This topic is discussed in more detail in

* [Specification.Wallet.UTxO](Specification/Wallet/UTxO.lagda.md)

Here, we only introduce basic concepts of UTxO management,
as we want to focus on the relation between customer addresses
and funds in the wallet.

The basics of `UTxO` managed are as follows:
The total UTxO of the wallet that can be spent is provided by the function

    totalUTxO : WalletState ‚Üí UTxO

A transaction of type `Tx` may spend some outputs
and create new ones.
The function

    applyTxToUTxO : (Address ‚Üí Bool) ‚Üí Tx ‚Üí UTxO ‚Üí UTxO

applies the transaction to the `UTxO`.
Here, the first argument is a predicate that specifies which output
addresses of a transactions belong to the wallet.
For the Deposit Wallet, we derive this from the wallet state as

    isOurs : WalletState ‚Üí Address ‚Üí Bool


Now, we discuss the entire `WalletState`.
First, we consider the predicate `isOurs`.
We require that all known customer addresses belong to the wallet

```agda
      prop-knownCustomerAddress-isOurs
        : ‚àÄ (addr : Address) (s : WalletState)
        ‚Üí knownCustomerAddress addr s ‚â° True
        ‚Üí isOurs s addr ‚â° True
```

However, there may be additional addresses belonging to the wallet,
in particular change addresses.

In order to apply a `Tx` to the entire `WalletState`,
we use the function

    applyTx : WalletState ‚Üí Tx ‚Üí UTxO ‚Üí UTxO

We require that this function
is equivalent to `applyTxToUTxO` on the `totalUTxO`:

```agda
      prop-totalUTxO-applyTx
        : ‚àÄ (s : WalletState) (tx : Tx)
        ‚Üí totalUTxO (applyTx tx s)
          ‚â° applyTxToUTxO (isOurs s) tx (totalUTxO s)
```

Also, as discussed previously,
we require that the mapping between customers and addresses
remain unchanged:

```agda
      prop-listCustomers-applyTx
        : ‚àÄ (s : WalletState) (tx : Tx)
        ‚Üí listCustomers (applyTx tx s) ‚â° listCustomers s
```

### Tracking incoming funds

The wallet tracks all addresses in `listCustomers`
whenever new blocks are incorporated into the wallet state.

For this purpose of tracking, we introduce an operation

    getCustomerHistory : WalletState ‚Üí Customer ‚Üí List TxSummary

which returns a list of transaction summaries. For a given transaction, such a summary reports the total `Value` spend or received at a specific address.

    record ValueTransfer : Set where
      field
        spent    : Value
        received : Value

    open ValueTransfer

    TxSummary : Set
    TxSummary = Slot √ó TxId √ó ValueTransfer

Note that the customer deposit wallet does not support
delegation and reward accounts, and the `spent` value
can only be spent from transaction outputs.

The function `getCustomerHistory` allows users to detect incoming
transfers by observing the `received` value.

TODO: Expand on `summarize`.

The behavior of this function is best specified in terms of a function

```agda
      summarize : WalletState ‚Üí Tx ‚Üí List (Address √ó TxSummary)

    getAddressSummary
      : Address ‚Üí List (Address √ó TxSummary) ‚Üí List TxSummary
    getAddressSummary address =
      map snd ‚àò filter (Œª x ‚Üí fst x == address)
```

which summarizes a single transaction. Specifically, the result of `getCustomerHistory` an aggregate of all previous transaction summaries.

```agda
    field
      prop-getAddressHistory-summary
        : ‚àÄ (s : WalletState)
            (c : Customer)
            (address : Address)
            (tx : Tx)
        ‚Üí (c , address) ‚àà listCustomers s
        ‚Üí getCustomerHistory (applyTx tx s) c
          ‚â° (getAddressSummary address (summarize s tx))
              ++ getCustomerHistory s c
```

Importantly, we only track an address if and only if it is a `knownCustomerAddress`.

```agda
      prop-tx-known-address
        : ‚àÄ (address : Address)
            (s : WalletState)
            (tx : Tx)
        ‚Üí (knownCustomerAddress address s ‚â° True)
        ‚áî (address ‚àà map fst (summarize s tx))
```

### Creating transactions

Finally, we expose an operation

    createPayment
      : List (Address √ó Value)
      ‚Üí PParams ‚Üí WalletState ‚Üí Maybe TxBody

which constructs a transaction that sends given values to given addresses.
Here, `PParams` are protocol parameters needed for computation the fee to
include in the `Tx`.

First, as the main purpose of a wallet is to be able to send funds,
it would be most desirable to require that this function always **succeeds**
in creating a transaction provided that the wallet has **sufficient funds**.
Unfortunately, however, we do not yet have an implementation
where we can prove this property. This topic is discussed in

* [Specification.Wallet.Payment](Specification/Wallet/Payment.lagda.md)

Second, the transaction sends funds as indicated

```agda
    field
      prop-createPayment-pays
        : ‚àÄ (s : WalletState)
            (pp : PParams)
            (destinations : List (Address √ó Value))
            (tx : TxBody)
          ‚Üí createPayment destinations pp s ‚â° Just tx
          ‚Üí isSubsetOf (outputs tx) destinations ‚â° True
```

Third, and most importantly, the operation `createPayment` never creates a transaction
whose `received` summary for any tracked index/address pair is non-zero.
In other words, `createPayment` uses change addresses that are distinct
from any address obtained via `createAddress`.

That said, `createPayment` is free to contribute to the `spent` summary of any address
‚Äî the deposit wallet spends funds from any address as it sees fit.

In other words, we have

```agda
    getAddress : (Address √ó Value) ‚Üí Address
    getAddress = fst

    field
      prop-createPayment-not-known
        : ‚àÄ (pp : PParams)
            (s  : WalletState)
            (destinations : List (Address √ó Value))
            (tx : TxBody)
        ‚Üí createPayment destinations pp s ‚â° Just tx
        ‚Üí ‚àÄ (address : Address)
          ‚Üí knownCustomerAddress address s ‚â° True
          ‚Üí ¬¨ (address ‚àà map fst destinations)
          ‚Üí ¬¨ (address ‚àà map getAddress (outputs tx))
```

